buildscript {
    dependencies {
        classpath plgs.defs.android.androidTools
        classpath plgs.defs.common.swaggerCodegen
    }
}

def OPENAPI_SOURCE = "$rootDir/kleo-api-v5.yaml"

apply plugin: 'com.android.application'
apply plugin: 'org.hidetake.swagger.generator'
apply from: "$rootDir/build-utils.gradle"

dependencies {
    compile 'com.wdullaer:materialdatetimepicker:3.5.0'

    implementation deps.defs.common.apacheLang3

    swaggerCodegen deps.defs.common.swaggerCodegenCli

    implementation deps.defs.android.appcompat
    implementation deps.defs.android.support
    implementation deps.defs.android.supportDesign
    implementation deps.defs.android.constraintLayout
    implementation deps.defs.android.playServicesAuth

    implementation deps.defs.android.swaggerAnnotations
    implementation deps.defs.android.retrofit
    implementation deps.defs.android.rxAndroidJava
    implementation (deps.defs.android.oltuOAuth2) {
        exclude group: 'org.apache.oltu.oauth2', module: 'org.apache.oltu.oauth2.common'
    }
    implementation deps.defs.android.gsonFire
    implementation deps.defs.android.jwtDecode

    implementation deps.defs.android.web3j
    implementation deps.defs.android.rxAndroidJava1

    testImplementation deps.defs.common.junit
    testImplementation deps.defs.common.assertj
    testImplementation deps.defs.common.mockito
}

android {
    flavorDimensions "default"

    compileOptions {
        sourceCompatibility configs.compiler.sourceCompatibility
        targetCompatibility configs.compiler.targetCompatibility
    }

    buildToolsVersion configs.android.buildToolsVersion
    compileSdkVersion configs.android.compileSdkVersion

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/rxjava.properties'
    }

    defaultConfig {
        applicationId "de.tum.kleo"
        minSdkVersion configs.android.minSdkVersion
        targetSdkVersion configs.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        multiDexEnabled true

        buildConfigField 'String', 'ETHEREUM_INFURA', "\"$System.env.ETHEREUM_INFURA\""
        buildConfigField 'String', 'ETHEREUM_ATTENDANCE_TRACKER_CONTRACT_ADDRESS',
            "\"$System.env.ETHEREUM_ATTENDANCE_TRACKER_ADDRESS\""
        buildConfigField 'String', 'ETHEREUM_ATTENDANCE_TRACKER_CONTRACT_URL',
            "\"$System.env.ETHEREUM_ATTENDANCE_TRACKER_URL\""
    }

    productFlavors {
        prod {
            buildConfigField 'String', 'BACKEND_BASE_URL', '"http://kleo.herokuapp.com:8080/api/"'
            buildConfigField 'String', 'BACKEND_CLIENT_ID', '"kleo-client"'
            buildConfigField 'String', 'BACKEND_CLIENT_SECRET', '""'
        }

        dev {
            buildConfigField 'String', 'BACKEND_BASE_URL', "\"http://${getIP()}:8080/api/\""
            buildConfigField 'String', 'BACKEND_CLIENT_ID', '"kleo-client"'
            buildConfigField 'String', 'BACKEND_CLIENT_SECRET', '""'
        }
    }

    buildTypes {
        release {
            minifyEnabled configs.android.minifyEnabled
        }

        debug {
            minifyEnabled false
        }
    }

    lintOptions {
        tasks.lint.enabled = configs.android.lintEnabled
    }

    sourceSets.main.java.srcDirs += file("$buildDir/swagger-code-kleo/src/main/java")
}

swaggerSources {
    kleo {
        inputFile = file(OPENAPI_SOURCE)
        code {
            language = 'java'
            configFile = file('swagger.json')
        }
    }
}

swaggerSources.kleo.code.doLast {
    delete fileTree(dir: "$buildDir/swagger-code-kleo").exclude("src/")
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.javaCompiler.dependsOn(swaggerSources.kleo.code)
    }
}
